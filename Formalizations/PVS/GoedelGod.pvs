GoedelGod: THEORY
 BEGIN

  i: TYPE+ 

  mu: TYPE+

  r: (symmetric?[i])

  sigma: TYPE = PRED[i]

  phi, psi: VAR sigma

  Phi, Psi: VAR [mu -> sigma]

  v, w: VAR i

  x, y: VAR mu

  ~: [sigma -> sigma] = LAMBDA phi: LAMBDA w: NOT phi(w);

  /\: [sigma, sigma -> sigma] =
      LAMBDA phi, psi: LAMBDA w: phi(w) AND psi(w);

  \/: [sigma, sigma -> sigma] = LAMBDA phi, psi: LAMBDA w: phi(w) OR psi(w);

  =>: [sigma, sigma -> sigma] =
      LAMBDA phi, psi: LAMBDA w: phi(w) IMPLIES psi(w);

  <=>: [sigma, sigma -> sigma] =
      LAMBDA phi, psi: LAMBDA w: phi(w) IFF psi(w);

  mforall: [[mu -> sigma] -> sigma] =
      LAMBDA Phi: LAMBDA w: FORALL x: Phi(x)(w);

  mexist: [[mu -> sigma] -> sigma] =
      LAMBDA Phi: LAMBDA w: EXISTS x: Phi(x)(w);

  Π: [[[mu -> sigma] -> sigma] -> sigma] =
      LAMBDA (P: [[mu -> sigma] -> sigma]):
        LAMBDA w: FORALL (z: [mu -> sigma]): P(z)(w);

  ☐ : [sigma -> sigma] =
      LAMBDA phi: LAMBDA w: FORALL v: NOT r(w, v) OR phi(v);

  ◇ : [sigma -> sigma] =
      LAMBDA phi: LAMBDA w: EXISTS v: r(w, v) AND phi(v);

  ⊨: PRED[sigma] = LAMBDA phi: FORALL w: phi(w);

  dia: LEMMA  ⊨(◇(~phi)<=>(~(☐(phi))))

  P: [[mu -> sigma] -> sigma];

  A1: AXIOM ⊨(Π(LAMBDA Phi: ~P(Phi) <=> P(LAMBDA x: ~Phi(x))));

  A1a: COROLLARY ⊨(Π(LAMBDA Phi: P(LAMBDA x: ~Phi(x)) => ~P(Phi)));

  A1b: COROLLARY ⊨(Π(LAMBDA Phi: ~P(Phi) => P(LAMBDA x: ~Phi(x))));

  A2: AXIOM
    ⊨(Π(LAMBDA Phi:
                  Π(LAMBDA Psi:
                          P(Phi) /\
                           ☐ (mforall(LAMBDA x: Phi(x) => Psi(x)))
                           => P(Psi))));

  T1: THEOREM ⊨(Π(LAMBDA Phi: P(Phi) => ◇ (mexist(Phi))));

  G: [mu -> sigma] = LAMBDA x: Π(LAMBDA Phi: P(Phi) => Phi(x));

  A3: AXIOM ⊨(P(G));

  C: COROLLARY ⊨(◇ (mexist(G)));

  A4: AXIOM ⊨(Π(LAMBDA Phi: P(Phi) => ☐ (P(Phi))));

  ess: [[mu -> sigma], mu -> sigma] =
      LAMBDA Phi, x:
        Phi(x) /\
         Π(LAMBDA Psi:
                 Psi(x) => ☐ (mforall(LAMBDA y: Phi(y) => Psi(y))));

  T2: THEOREM ⊨(mforall(LAMBDA x: G(x) => ess(G, x)));

  NE: [mu -> sigma] =
      LAMBDA x: Π(LAMBDA Phi: ess(Phi, x) => ☐ (mexist(Phi)))

  A5: AXIOM ⊨(P(NE))

  T3: THEOREM ⊨(☐ (mexist(G)))

  C2: COROLLARY ⊨(mexist(G))

  MC: LEMMA ⊨(phi => ☐ (phi))
 END GoedelGod
